#!/usr/bin/perl
use strict;
use warnings;

sub assertIp($);
sub assertMac($);
sub parseConfig($);
sub hostsMarker($);
sub getHosts($);
sub setHosts($$$);
sub sudoSetHosts($$$);
sub maybeUpdateHosts($$);
sub ifup($$);
sub sudoIfup($$);
sub usb($);
sub guessIps($);
sub wifi($);

my $usage = "Usage:
    $0 CONF_FILE [-u USER]
      setup hosts file, init usb devices, and print hostname
    $0 CONF_FILE [-u USER] -vnc [ARGS]
      setup hosts file, init usb devices, and call vncCmd
      e.g.: for n9-vnc: `$0 -vnc rotate90 -- -quality 0`
    $0 CONF_FILE [-u USER] -s [CMD ARG ARG..]
      setup hosts file, init usb devices, and ssh to host
    $0 CONF_FILE [-u USER] -b BASHCMD
      setup hosts file, init usb devices, and ssh with bash -c SHELLCMD
    $0 CONF_FILE --host
      print the hostname and exit, doing nothing else
    $0 --set-hosts HOSTNAME MARKER IP
      simply update the hosts file line
    $0 --ifup DEV IP
      ifup the DEV with the IP
    USER is the user that will be used in -s, -b, and -vnc\n";

sub main(@){
  if(@_ == 4 and $_[0] eq '--set-hosts'){
    setHosts $_[1], $_[2], $_[3];
    exit 0;
  }elsif(@_ == 3 and $_[0] eq '--ifup'){
    ifup $_[1], $_[2];
    exit 0;
  }
  my $confFile = shift;
  die $usage if not defined $confFile or not -e $confFile;
  my %config = %{parseConfig $confFile};
  die "hostname not defined in conf\n" if not defined $config{hostname};
  my $hostname = $config{hostname};
  if(@_ == 1 and $_[0] eq '--host'){
    print "$hostname\n";
    exit 0;
  }

  my $sshUser;
  if(@_ > 1 and $_[0] eq '-u'){
    shift;
    $sshUser = shift;
  }elsif(defined $config{defaultUser}){
    $sshUser = $config{defaultUser};
  }else{
    $sshUser = 'root';
  }

  my $vncCmd;
  my $isSsh = 0;
  my @sshCmd;
  if(@_ > 0 and $_[0] eq '-vnc'){
    shift;
    if(defined $config{vncCommand}){
      $vncCmd = $config{vncCommand};
    }else{
      $vncCmd = "vnc $sshUser\@$hostname";
    }
    if(@_ > 0){
      $vncCmd .= " @_";
    }elsif(defined $config{vncDefaultArgs}){
      $vncCmd .= " $config{vncDefaultArgs}";
    }
  }elsif(@_ > 0 and $_[0] eq '-s'){
    shift;
    $isSsh = 1;
    @sshCmd = @_;
  }elsif(@_ > 0 and $_[0] eq '-b'){
    shift;
    $isSsh = 1;
    die "-b missing command\n" if @_ == 0;
    my $c = "@_";
    $c =~ s/'/'\\''/g;
    @sshCmd = ("bash -c '$c'");
  }elsif(@_ > 0){
    die $usage;
  }

  my $ok;
  $ok = usb \%config if not $ok;
  $ok = wifi \%config if not $ok;
  $ok = guessIps \%config if not $ok;
  die "Could not get an ip\n" if not $ok;

  if(defined $vncCmd){
    exec $vncCmd;
  }elsif($isSsh){
    exec "ssh", "$sshUser\@$hostname", @sshCmd;
  }else{
    print "$hostname\n";
  }
}

sub assertIp($){
  my $ip = shift;
  if($ip !~ /^\d+\.\d+\.\d+\.\d+$/){
    die "'$ip' is not an ip4 address\n"
  }
}

sub assertMac($){
  my $mac = shift;
  my $seg = "[0-9a-f]{2}";
  if($mac !~ /^$seg:$seg:$seg:$seg:$seg:$seg$/i){
    die "'$mac' is not a mac address\n"
  }
}

sub parseConfig($){
  my $file = shift;
  open FH, "< $file" or die "Couldnt open conf $file\n";
  my @lines = <FH>;
  close FH;

  my %mapKeys = map {$_ => 1} qw(macIp ssidIp);
  my %listKeys = map {$_ => 1} qw(mac ipGuess);
  my %singleKeys = map {$_ => 1} qw(
    hostname vncCommand vncDefaultArgs defaultUser
    usbLocalIp usbRemoteIp
  );
  my %assertIp = map {$_ => 1} qw(ipGuess usbLocalIp usbRemoteIp);
  my %assertMac = map {$_ => 1} qw(mac);
  my %assertIpLeft = map {$_ => 1} qw();
  my %assertMacLeft = map {$_ => 1} qw(macIp);
  my %assertIpRight = map {$_ => 1} qw(macIp ssidIp);
  my %assertMacRight = map {$_ => 1} qw();

  my %config;
  for my $line(@lines){
    $line =~ s/#.*//;
    next if $line =~ /^\s*$/;
    die "Malformed conf: $line\n" if $line !~ /^\s*([a-z]+)\s*=\s*(.+?)\s*$/i;
    my ($key, $val) = ($1, $2);

    if(defined $mapKeys{$key}){
      if($val !~ /^ \s* (\S* | "[^"]*" ) \s* :: \s* (\S* | "[^"]*") \s*$/x){
        die "Malformed map conf entry $key = $val\n";
      }
      my ($valLeft, $valRight) = ($1, $2);
      assertIp $valLeft if defined $assertIpLeft{$key};
      assertMac $valLeft if defined $assertMacLeft{$key};
      assertIp $valRight if defined $assertIpRight{$key};
      assertMac $valRight if defined $assertMacRight{$key};

      $key .= "Map";
      $config{$key} = {} if not defined $config{$key};
      ${$config{$key}}{$valLeft} = $valRight;
    }elsif(defined $listKeys{$key}){
      assertIp $val if defined $assertIp{$key};
      assertMac $val if defined $assertMac{$key};
      $key .= "List";
      $config{$key} = [] if not defined $config{$key};
      push @{$config{$key}}, $val;
    }elsif(defined $singleKeys{$key}){
      assertIp $val if defined $assertIp{$key};
      assertMac $val if defined $assertMac{$key};
      die "Duplicate single conf entry $key\n" if defined $config{$key};
      $config{$key} = $val;
    }else{
      die "Unknown conf entry: $key\n";
    }
  }

  return \%config;
}

sub hostsMarker($){
  my %config = %{shift()};
  my $hostname = $config{hostname};
  return "ipmagic:$hostname";
}

sub getHosts($){
  my %config = %{shift()};
  my $hostsMarker = hostsMarker \%config;
  my $hostname = $config{hostname};
  for my $line(`cat /etc/hosts`){
    if($line =~ /^(\d+\.\d+\.\d+\.\d+)\s*$hostname\s*#$hostsMarker$/){
      return $1;
    }
  }
  return undef;
}

sub setHosts($$$){
  my ($hostname, $hostsMarker, $ip) = @_;
  my @lines = `cat /etc/hosts`;
  for my $line(@lines){
    if($line =~ /^(\d+\.\d+\.\d+\.\d+)\s*$hostname\s*#$hostsMarker$/){
      $line = '';
    }
  }
  if(defined $ip and $ip =~ /^\d+\.\d+\.\d+\.\d+$/){
    push @lines, "$ip\t$hostname#$hostsMarker\n";
  }

  open FH, "> /etc/hosts" or die "Couldnt open /etc/hosts for writing\n";
  print FH @lines;
  close FH;
}
sub sudoSetHosts($$$){
  my ($hostname, $hostsMarker, $ip) = @_;
  system "sudo", $0, "--set-hosts", $hostname, $hostsMarker, $ip;
}

sub maybeUpdateHosts($$){
  my %config = %{shift()};
  my $hostsMarker = hostsMarker \%config;
  my $hostname = $config{hostname};
  my $ip = shift;
  my $old = getHosts \%config;
  if(not defined $ip){
    if(defined $old){
      print STDERR "removing $hostname line from /etc/hosts\n";
    }else{
      return;
    }
  }else{
    if(not defined $old or $old ne $ip){
      print STDERR "replacing $hostname line in /etc/hosts\n";
    }else{
      return;
    }
  }

  sudoSetHosts $hostname, $hostsMarker, $ip;
}

sub ifup($$){
  my ($dev, $ip) = @_;
  system "ifconfig"
    . " $dev $ip netmask 255.255.255.0 up"
    . " > /dev/null"
    ;
}
sub sudoIfup($$){
  my ($dev, $ip) = @_;
  system "sudo", $0, "--ifup", $dev, $ip;
}

sub usb($){
  my %config = %{shift()};
  if(not defined $config{usbLocalIp} or not defined $config{usbRemoteIp}){
    return 0;
  }
  my $localIp = $config{usbLocalIp};
  my $remoteIp = $config{usbRemoteIp};

  my $ifconfig = `ifconfig -a`;
  my @macs = @{$config{macList}};
  foreach my $mac(@macs){
    #if($ifconfig =~ /^([a-z0-9_]+).*$mac\s*$/mi){
    if($ifconfig =~ /^(usb\d+)/mi){ #assume any usb, because mac keeps changing
      my $dev = $1;
      $ifconfig = `ifconfig $dev`;
      if($ifconfig !~ /inet addr:$localIp/){
        print STDERR "USB DEVICE UP\n";
        sudoIfup $dev, $localIp;
      }
      maybeUpdateHosts \%config, $remoteIp;
      return 1;
    }
  }
  return 0;
}

sub guessIps($){
  my %config = %{shift()};
  my $hostname = $config{hostname};
  maybeUpdateHosts \%config, undef;
  if(defined $config{ipGuessList}){
    my @ips = @{$config{ipGuessList}};
    system 'ensure-host', $hostname, @ips;
    if($? == 0){
      return 1;
    }else{
      print STDERR "Error ensuring host: $!\n";
    }
  }
  return 0;
}

sub wifi($){
  my %config = %{shift()};
  my %macIpMap = %{$config{macIpMap}} if defined $config{macIpMap};
  my %ssidIpMap = %{$config{ssidIpMap}} if defined $config{ssidIpMap};

  my $iwconfig = `iwconfig 2>/dev/null`;
  my $mac = lc $1 if $iwconfig =~ /Access Point: ([a-z0-9:]+)/i;
  my $ssid = lc $1 if $iwconfig =~ /ESSID:"(.*)"/;
 
  my $ip;
  $ip = $macIpMap{$mac} if defined $mac and not defined $ip;
  $ip = $ssidIpMap{$ssid} if defined $ssid and not defined $ip;
  if(defined $ip){
    maybeUpdateHosts \%config, $ip;
    return 1;
  }
  return 0;
}

&main(@ARGV);
