#!/usr/bin/perl
use strict;
use warnings;

sub assertIp($);
sub assertMac($);
sub parseConfig($);
sub hostsMarker($);
sub getHosts($);
sub setHosts($$$);
sub sudoSetHosts($$$);
sub maybeUpdateHosts($$);
sub quickPingable($);
sub ifup($$);
sub sudoIfup($$);
sub usb($);
sub twip($);
sub guess($);
sub wifi($);

my $VERBOSE = 0;

my $usage = "Usage:
    $0 CONF_FILE [-v|--verbose] [-u USER]
      setup hosts file, init usb devices, and print hostname
    $0 CONF_FILE [-v|--verbose] [-u USER] -vnc [ARGS]
      setup hosts file, init usb devices, and call vncCmd
      e.g.: for n9-vnc: `$0 -vnc rotate90 -- -quality 0`
    $0 CONF_FILE [-v|--verbose] [-u USER] -s [CMD ARG ARG..]
      setup hosts file, init usb devices, and ssh to host
    $0 CONF_FILE [-v|--verbose] [-u USER] -b BASHCMD
      setup hosts file, init usb devices, and ssh with bash -c SHELLCMD
    $0 CONF_FILE [-v|--verbose] --host
      print the hostname and exit, doing nothing else
    $0 --set-hosts HOSTNAME MARKER IP
      simply update the hosts file line
    $0 --ifup DEV IP
      ifup the DEV with the IP
    USER is the user that will be used in -s, -b, and -vnc\n";

sub main(@){
  if(@_ == 4 and $_[0] eq '--set-hosts'){
    setHosts $_[1], $_[2], $_[3];
    exit 0;
  }elsif(@_ == 3 and $_[0] eq '--ifup'){
    ifup $_[1], $_[2];
    exit 0;
  }
  my $confFile = shift;
  die $usage if not defined $confFile or not -e $confFile;

  $VERBOSE = shift if @_ > 0 and $_[0] =~ /^(-v|--verbose)$/;

  my %config = %{parseConfig $confFile};
  die "hostname not defined in conf\n" if not defined $config{hostname};
  my $hostname = $config{hostname};
  if(@_ == 1 and $_[0] eq '--host'){
    print "$hostname\n";
    exit 0;
  }

  my $sshUser;
  if(@_ > 1 and $_[0] eq '-u'){
    shift;
    $sshUser = shift;
  }elsif(defined $config{defaultUser}){
    $sshUser = $config{defaultUser};
  }else{
    $sshUser = 'root';
  }


  my ($isVnc, $isSsh) = (0, 0);
  my $vncCmd;
  my @sshCmd;
  if(@_ > 0 and $_[0] eq '-vnc'){
    $isVnc = shift;
    if(defined $config{vncCommand}){
      $vncCmd = $config{vncCommand};
    }else{
      $vncCmd = "vnc $sshUser\@$hostname";
    }
    if(@_ > 0){
      $vncCmd .= " @_";
    }elsif(defined $config{vncDefaultArgs}){
      $vncCmd .= " $config{vncDefaultArgs}";
    }
  }elsif(@_ > 0 and $_[0] eq '-s'){
    $isSsh = shift;
    @sshCmd = @_;
  }elsif(@_ > 0 and $_[0] eq '-b'){
    $isSsh = shift;
    die "-b missing command\n" if @_ == 0;
    my $c = "@_";
    $c =~ s/'/'\\''/g;
    @sshCmd = ("bash -c '$c'");
  }elsif(@_ > 0){
    die $usage;
  }

  my ($ip, $mode);
  ($ip, $mode) = (usb(\%config), 'usb') if not defined $ip;
  ($ip, $mode) = (wifi(\%config), 'wifi') if not defined $ip;
  ($ip, $mode) = (twip(\%config), 'twip') if not defined $ip;
  ($ip, $mode) = (guess(\%config), 'guess') if not defined $ip;
  $mode = undef if not defined $ip;

  if(not defined $ip and not quickPingable $hostname){
    die "No ip found, and could not ping $hostname\n";
  }else{
    maybeUpdateHosts \%config, $ip;
  }

  if($isVnc){
    print "$vncCmd\n" if $VERBOSE;
    exec $vncCmd;
  }elsif($isSsh){
    if($mode eq 'twip' and defined $config{twipPort}){
      my $port = $config{twipPort};
      print "sshing on port $port\n" if $VERBOSE;
      @sshCmd = ("-p", $port, @sshCmd);
    }
    my @cmd = ("ssh", "$sshUser\@$hostname", @sshCmd);
    print "@cmd\n" if $VERBOSE;
    exec @cmd;
  }else{
    print "$hostname\n";
  }
}

sub assertIp($){
  my $ip = shift;
  if($ip !~ /^\d+\.\d+\.\d+\.\d+$/){
    die "'$ip' is not an ip4 address\n"
  }
}

sub assertMac($){
  my $mac = shift;
  my $seg = "[0-9a-f]{2}";
  if($mac !~ /^$seg:$seg:$seg:$seg:$seg:$seg$/i){
    die "'$mac' is not a mac address\n"
  }
}

sub parseConfig($){
  my $file = shift;
  open FH, "< $file" or die "Couldnt open conf $file\n";
  my @lines = <FH>;
  close FH;

  my %mapKeys = map {$_ => 1} qw(macIp ssidIp);
  my %listKeys = map {$_ => 1} qw(mac ipGuess);
  my %singleKeys = map {$_ => 1} qw(
    hostname twipPort vncCommand vncDefaultArgs defaultUser
    usbLocalIp usbRemoteIp
  );
  my %assertIp = map {$_ => 1} qw(ipGuess usbLocalIp usbRemoteIp);
  my %assertMac = map {$_ => 1} qw(mac);
  my %assertIpLeft = map {$_ => 1} qw();
  my %assertMacLeft = map {$_ => 1} qw(macIp);
  my %assertIpRight = map {$_ => 1} qw(macIp ssidIp);
  my %assertMacRight = map {$_ => 1} qw();

  my %config;
  for my $line(@lines){
    $line =~ s/#.*//;
    next if $line =~ /^\s*$/;
    die "Malformed conf: $line\n" if $line !~ /^\s*([a-z]+)\s*=\s*(.+?)\s*$/i;
    my ($key, $val) = ($1, $2);

    if(defined $mapKeys{$key}){
      if($val !~ /^ \s* (\S* | "[^"]*" ) \s* :: \s* (\S* | "[^"]*") \s*$/x){
        die "Malformed map conf entry $key = $val\n";
      }
      my ($valLeft, $valRight) = ($1, $2);
      assertIp $valLeft if defined $assertIpLeft{$key};
      assertMac $valLeft if defined $assertMacLeft{$key};
      assertIp $valRight if defined $assertIpRight{$key};
      assertMac $valRight if defined $assertMacRight{$key};

      $key .= "Map";
      $config{$key} = {} if not defined $config{$key};
      ${$config{$key}}{$valLeft} = $valRight;
    }elsif(defined $listKeys{$key}){
      assertIp $val if defined $assertIp{$key};
      assertMac $val if defined $assertMac{$key};
      $key .= "List";
      $config{$key} = [] if not defined $config{$key};
      push @{$config{$key}}, $val;
    }elsif(defined $singleKeys{$key}){
      assertIp $val if defined $assertIp{$key};
      assertMac $val if defined $assertMac{$key};
      die "Duplicate single conf entry $key\n" if defined $config{$key};
      $config{$key} = $val;
    }else{
      die "Unknown conf entry: $key\n";
    }
  }

  return \%config;
}

sub hostsMarker($){
  my %config = %{shift()};
  my $hostname = $config{hostname};
  return "ipmagic:$hostname";
}

sub getHosts($){
  my %config = %{shift()};
  my $hostsMarker = hostsMarker \%config;
  my $hostname = $config{hostname};
  for my $line(`cat /etc/hosts`){
    if($line =~ /^(\d+\.\d+\.\d+\.\d+)\s*$hostname\s*#$hostsMarker$/){
      return $1;
    }
  }
  return undef;
}

sub setHosts($$$){
  my ($hostname, $hostsMarker, $ip) = @_;
  my @lines = `cat /etc/hosts`;
  for my $line(@lines){
    if($line =~ /^(\d+\.\d+\.\d+\.\d+)\s*$hostname\s*#$hostsMarker$/){
      $line = '';
    }
  }
  if(defined $ip and $ip =~ /^\d+\.\d+\.\d+\.\d+$/){
    push @lines, "$ip\t$hostname#$hostsMarker\n";
  }

  open FH, "> /etc/hosts" or die "Couldnt open /etc/hosts for writing\n";
  print FH @lines;
  close FH;
}
sub sudoSetHosts($$$){
  my ($hostname, $hostsMarker, $ip) = @_;
  system "sudo", $0, "--set-hosts", $hostname, $hostsMarker, $ip;
}

sub maybeUpdateHosts($$){
  my %config = %{shift()};
  my $ip = shift;
  my $hostsMarker = hostsMarker \%config;
  my $hostname = $config{hostname};
  my $old = getHosts \%config;
  if(not defined $ip){
    if(defined $old){
      print STDERR "removing $hostname line from /etc/hosts\n";
    }else{
      return;
    }
  }else{
    if(not defined $old or $old ne $ip){
      print STDERR "replacing $hostname line in /etc/hosts\n";
    }else{
      return;
    }
  }

  sudoSetHosts $hostname, $hostsMarker, $ip;
}

sub quickPingable($){
  system "ping $_[0] -c 1 -w 1 >/dev/null 2>/dev/null";
  return $? == 0;
}

sub ifup($$){
  my ($dev, $ip) = @_;
  system "ifconfig"
    . " $dev $ip netmask 255.255.255.0 up"
    . " > /dev/null"
    ;
}
sub sudoIfup($$){
  my ($dev, $ip) = @_;
  system "sudo", $0, "--ifup", $dev, $ip;
}

sub usb($){
  my %config = %{shift()};
  if(not defined $config{usbLocalIp} or not defined $config{usbRemoteIp}){
    return undef;
  }
  print "Trying usb\n" if $VERBOSE;
  my $localIp = $config{usbLocalIp};
  my $remoteIp = $config{usbRemoteIp};

  my $ifconfig = `ifconfig -a`;
  my @macs = @{$config{macList}};
  foreach my $mac(@macs){
    #if($ifconfig =~ /^([a-z0-9_]+).*$mac\s*$/mi){
    if($ifconfig =~ /^(usb\d+)/mi){ #assume any usb, because mac keeps changing
      my $dev = $1;
      print "USB network device $dev found, assuming this is it\n" if $VERBOSE;
      $ifconfig = `ifconfig $dev`;
      if($ifconfig !~ /inet addr:$localIp/){
        print STDERR "USB DEVICE UP\n";
        sudoIfup $dev, $localIp;
      }
      print "  success!\n" if $VERBOSE;
      return $remoteIp;
    }
  }
  return undef;
}

sub twip($){
  my %config = %{shift()};
  if(defined $config{twipPort}){
    print "Trying twip\n" if $VERBOSE;
    my $ip = `twipc --fetch`;
    chomp $ip;
    print "  got $ip\n";
    if($ip =~ /^\d+ \. \d+ \. \d+ \. \d+$/x){
      print "  success!\n" if $VERBOSE;
      return $ip;
    }
  }
  return undef;
}

sub guess($){
  my %config = %{shift()};
  my $hostname = $config{hostname};
  if(defined $config{ipGuessList}){
    print "Trying guesses\n" if $VERBOSE;
    for my $ip(@{$config{ipGuessList}}){
      print "  guess $ip\n" if $VERBOSE;
      if(quickPingable $ip){
        print "  success!\n" if $VERBOSE;
        return $ip;
      }
    }
  }
  return undef;
}

sub wifi($){
  my %config = %{shift()};
  if(not defined $config{macIpMap} and not defined $config{ssidIpMap}){
    return undef;
  }
  my %macIpMap = %{$config{macIpMap}} if defined $config{macIpMap};
  my %ssidIpMap = %{$config{ssidIpMap}} if defined $config{ssidIpMap};

  my $iwconfig = `iwconfig 2>/dev/null`;
  my $mac = lc $1 if $iwconfig =~ /Access Point: ([a-z0-9:]+)/i;
  my $ssid = lc $1 if $iwconfig =~ /ESSID:"(.*)"/;
  print "Trying wifi  {mac: $mac   ssid: $ssid}\n" if $VERBOSE;
 
  my $ip;
  $ip = $macIpMap{$mac} if defined $mac and not defined $ip;
  $ip = $ssidIpMap{$ssid} if defined $ssid and not defined $ip;
  if(defined $ip){
    print "  success!\n" if $VERBOSE;
    return $ip;
  }
  return undef;
}

&main(@ARGV);
