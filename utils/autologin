#!/usr/bin/perl
use strict;
use warnings;

my $tty = '6';
my $profileFile = "$ENV{HOME}/.profile";

my $user = $ENV{SUDO_USER};
if(not defined $user){
  $user = $ENV{USER};
}
if($user eq "root" or length $user == 0){
  die "USER or SUDO_USER var must be set and not root"
    . " {e.g.: SUDO_USER=john $0}\n";
}

sub run(@);
sub setupInit();
sub setupProfile();

sub main(@){
  die "Usage: $0\n" if @_ > 0;

  setupInit();
  setupProfile();
  my @displayManagers = qw(lxdm gdm kdm lightdm);
  for my $displayManager(@displayManagers){
    run "sudo apt-get remove --purge $displayManager";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

sub setupInit(){
  my $initLine = ''
    . "$tty:2345:respawn:"
    . "/bin/login -f $user tty$tty"
    . " </dev/tty$tty"
    . " >/dev/tty$tty"
    . " 2>&1"
    . "\n";

  my @lines = `cat /etc/inittab`;
  my $found = 0;
  for my $line(@lines){
    $line = '' if $line =~ /^#$initLine/;
    next if $line =~ /^\s*#/;
    if(not $found and $line =~ /tty$tty/){
      if($line eq $initLine){
        print "init tab line unchanged\n";
      }else{
        print "$line=>\n$initLine";
        $line = "#$line$initLine";
      }
      $found = 1;
    }
  }
  if(not $found){
    print "appending $initLine";
    push @lines, $initLine;
  }
  my $tab = "/etc/inittab";
  open FH, "| sudo tee $tab >/dev/null" or die "Couldnt write to $tab\n";
  print FH @lines;
  close FH;
}

sub setupProfile(){
  my $autoLoginSection = ''
    . "##AUTOLOGIN START##\n"
    . "if [ -z \"\$DISPLAY\" ]; then\n"
    . "  if [ \"\$(tty)\" == \"/dev/tty$tty\" ]; then\n"
    . "    exec startx\n"
    . "  fi\n"
    . "fi\n"
    . "##AUTOLOGIN END##\n"
    ;
  
  my $profile = `cat $profileFile`;
  $profile =~ s/##AUTOLOGIN START##.*##AUTOLOGIN END##\n?//s;
  $profile .= $autoLoginSection;

  print "Replacing autologin section in $profileFile with:\n";
  print "-----\n$autoLoginSection-----\n";

  open FH, "> $profileFile";
  print FH $profile;
  close FH;
}

&main(@ARGV);
