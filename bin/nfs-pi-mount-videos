#!/usr/bin/perl
use strict;
use warnings;

my $raspiIP = `pi --ip`;
chomp $raspiIP;
my $remoteDir = "/media/videos";
my $localDir = "/media/pi-videos";

my $usage = "Usage:
  $0 [-m]
    Restart nfs server on pi and mount $raspiIP:$remoteDir => $localDir:
  $0 -u
    Unmount $localDir
";

sub run(@);
sub isMountPoint($);

sub main(@){
  if(@_ == 0 or (@_ == 1 and $_[0] eq "-m")){
    run "sudo", "mkdir", "-p", $localDir if not -d $localDir;
    if(not isMountPoint $localDir){
      run "raspi", "-s", "
       /etc/init.d/nfs-kernel-server restart
      ";
      run "sudo", "mount", "$raspiIP:$remoteDir", $localDir;
    }
    die "$localDir is not mounted\n" if not isMountPoint $localDir;
  }elsif(@_ == 1 and $_[0] eq "-u"){
    run "sudo", "umount", $localDir if isMountPoint $localDir;
    die "Could not umount $localDir\n" if isMountPoint $localDir;
    run "sudo", "rmdir", $localDir if -d $localDir;
    die "$localDir still exists\n" if -d $localDir;
  }else{
    die $usage;
  }
}

sub run(@){
  print "@_\n";
  system @_;
  die "Error running: @_\n" if $? != 0;
}

sub isMountPoint($){
  system "mountpoint \"$_[0]\" >/dev/null 2>/dev/null";
  return $? == 0;
}

&main(@ARGV);
