NUM_SCROLL_LINES=20;
LINE_WIDTH=55;
 
wallpaper_image = Image("kells.png");
screen_width = Window.GetWidth();
screen_height = Window.GetHeight();
resized_wallpaper_image = wallpaper_image.Scale(screen_width,screen_height);
wallpaper_sprite = Sprite(resized_wallpaper_image);
wallpaper_sprite.SetZ(-100);
 
// Initialising text images and their positions
// 20 is the height (including line spacing) of each line
for (i=0; i < NUM_SCROLL_LINES; i++) {
  lines[i]= Image.Text("", 0, 0, 0);
  message_sprite[i] = Sprite();
  message_sprite[i].SetPosition(screen_width * 0.8, (screen_height * 0.1) + (i * 20), 10000);
}
 
// From ubuntu-logo
fun StringLength(string) {
 
  index = 0;
  str = String(string);
  while(str.CharAt(index)) index++;
  return index;
}
 
fun scroll_message_callback(text) {
 
   // Truncate the message if too long
   if (StringLength(text) > LINE_WIDTH) {
     text = text.SubString(0, LINE_WIDTH - 3);
     text += "...";
   }
 
   // Shift message one up
   for (i = 0; i < NUM_SCROLL_LINES - 1; i++) {
     lines[i] = lines[i+1];
   }
 
   // Create the image for the latest message
   lines[i] = Image.Text(text, 0, 0, 0);
 
   // Re-positioning the text images
   for (i = 0; i < NUM_SCROLL_LINES; i++) {
     message_sprite[i].SetImage(lines[i]);
   }
}
 
Plymouth.SetUpdateStatusFunction(scroll_message_callback);
